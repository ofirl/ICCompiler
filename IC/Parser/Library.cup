package IC.Parser;

import IC.AST.*;
import IC.*;
import java_cup.runtime.*;
import java.util.LinkedList;
import java.util.ArrayList;
import java.util.List;

parser code  {:
	/** Causes the parser to print every token it reads.
	 * This is useful for debugging.
	 */
	public boolean printTokens = true;
	
	private Lexer lexer;

	public parser(Lexer lexer) {
		super(lexer);
		this.lexer = lexer;
	}
	
	public int getLine() {
		return lexer.getLineNumber();
	}
	
	public void syntax_error(Symbol s) {
		Token tok = (Token) s;
		System.out.println("Line " + tok.getLine()+": Syntax error; unexpected " + tok);
	}
:}

// This is just for debugging
scan with {:
	Token t = lexer.next_token();
	if (printTokens)
		System.out.println(t.getLine() + ":" + t.getTagString() + ",\t" + t.getValue());
	return t; 
:};

////////////////////////////////////////////
// Symbol list (terminals and non-terminals)

/////			Terminals			/////

// general
terminal Integer 	NUM;
terminal String  	ID;
terminal String 	CLASS_ID;
terminal String 	STR; // an actual string like "Nice"

// operators
terminal PLUS, MINUS, MULTIPLY, DIVIDE, NEGATION, REMAINDER, BIG, BIGEQ;
terminal SMALL, SMALLEQ, EQ, EQCOMP, AND, OR, DOT, NOTEQCOMP;

// punctuation
terminal LP, RP, LB, RB, LCBR, RCBR, SEMI, COMMA;

// keywords
terminal CLASS, EXTENDS, STATIC, VOID, BOOLEAN, RETURN, IF, ELSE, WHILE;
terminal BREAK, CONTINUE, THIS, NEW, LENGTH, TRUE, FALSE, NULL;
terminal INT; // the keyword int
terminal STRING; // the keyword string

/////			Non-Terminals			/////

nonterminal ICClass libic;
nonterminal String libicpref;
nonterminal List<Method> libmethodList;
nonterminal LibraryMethod libmethod;
nonterminal Type type;
nonterminal Type typenvoid;
nonterminal List<Formal> formals;

//////////////////////////
// Precedence declarations

//////////////
// The grammar

libic ::= libicpref:c libmethodList:lst RCBR
		{: RESULT = new ICClass(cleft, c, new LinkedList<Field>(), lst); :}
		| libicpref:c RCBR
		{: RESULT = new ICClass(cleft, c, new LinkedList<Field>(), new LinkedList<Method>()); :}
;

libicpref ::= CLASS CLASS_ID:c LCBR
				{:
					if(!c.equals("Library")) throw new Exception("It Should be Library!!");
					RESULT = c;
				:}
;

libmethodList ::= libmethod:m
			{: RESULT = new LinkedList<Method>(); RESULT.add(m); :}
			| libmethodList:lst libmethod:m
			{: lst.add(m); RESULT = lst; :}
;

libmethod ::= STATIC typenvoid:t ID:method LP RP SEMI
			{: RESULT = new LibraryMethod(t,method,new ArrayList<Formal>()); :}
			| STATIC typenvoid:t ID:method LP formals:f RP SEMI
			{: RESULT = new LibraryMethod(t,method,f); :}
;

typenvoid ::= VOID:v {: RESULT = new PrimitiveType(vleft,DataTypes.VOID); :}
			| type:t {: RESULT = t; :}
;

type ::= INT:n
		{: RESULT = new PrimitiveType(nleft,DataTypes.INT); :}
		| BOOLEAN:b
		{: RESULT = new PrimitiveType(bleft,DataTypes.BOOLEAN); :}
		| STRING:s
		{: RESULT = new PrimitiveType(sleft,DataTypes.STRING); :}
		| CLASS_ID:c
		{: RESULT = new UserType(cleft,c); :}
		| type:t LB RB
		{: t.incrementDimension(); RESULT = t; :}
;

formals ::= type:t ID:i {: RESULT = new LinkedList<Formal>(); RESULT.add(new Formal(t,i)); :}
			| formals:f COMMA type:t ID:i  {: f.add(new Formal(t,i)); RESULT = f; :}
;
